source ~/.config/shell/rc

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# 
# Shell integrations
#

if [[ -f "/opt/homebrew/bin/brew" ]] then
  eval "$(/opt/homebrew/bin/brew shellenv)"
fi
  
# Load completions
mkdir -p ~/.zfunc/
fpath=(
  ~/.zfunc
  ${HOMEBREW_PREFIX}/share/zsh/site-functions
  "${fpath[@]}"
)

eval "$(fzf --zsh)"
eval "$(zoxide init zsh)"

export DIRENV_HOME="${HOME}/.config/direnv"
eval "$(direnv hook zsh)"

setopt auto_cd
alias -g ...='../..'
alias -g ....='../../..'

#
# User configuration
#

# correct max limits for open files and processes (done per bash session)
# also ~/Library/LaunchAgents/limit.maxfiles.plist
# https://docs.riak.com/riak/kv/2.1.4/using/performance/open-files-limit/#mac-os-x
ulimit -n 200000
ulimit -u 2048

path=(
    ${HOMEBREW_PREFIX}/opt/libpq/bin
    /Applications/Ghostty.app/Contents/MacOS
    $path
)

#
# Plugins
#

# Set the directory we want to store zinit and plugins
ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"

# Download Zinit, if it's not there yet
if [ ! -d "$ZINIT_HOME" ]; then
   mkdir -p "$(dirname $ZINIT_HOME)"
   git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
fi

# Source/Load zinit
source "${ZINIT_HOME}/zinit.zsh"

# Add in Powerlevel10k
zinit ice depth=1; zinit light romkatv/powerlevel10k

# Add in zsh plugins
zinit light zsh-users/zsh-syntax-highlighting
zinit light zsh-users/zsh-autosuggestions
zinit light atuinsh/atuin

# Add in snippets
zinit snippet OMZP::command-not-found

# Load completions
autoload -Uz compinit && compinit

zinit cdreplay -q

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.config/zsh/.p10k.zsh ]] || source ~/.config/zsh/.p10k.zsh

#
# Keybindings
#

bindkey -v
bindkey '^y' autosuggest-accept

# Use C-n/C-p and Up/Down keys
# to search history based on current input
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^p" up-line-or-beginning-search
bindkey "^n" down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

# edit command line in nvim
autoload edit-command-line
zle -N edit-command-line
bindkey -M vicmd '^v' edit-command-line

# 
# History
#

HISTFILE=~/.zsh_history
HISTSIZE=1000000
SAVEHIST=$HISTSIZE
HISTFILESIZE=1000000
HISTDUP=erase

setopt extended_history       # record timestamp of command in HISTFILE

# I don't want to share history between sessions,
# still in any new session I want to have complete history
# also, I have autin, that will suggest latest history in any session at any time.
setopt inc_append_history     # save new commands right away
# setopt append_history       # save all commands at once on shell exit
unsetopt share_history        # don't share command history data

setopt hist_ignore_space      # ignore commands that start with space
setopt hist_ignore_all_dups
setopt hist_save_no_dups
setopt hist_ignore_dups       # ignore duplicated commands history list
setopt hist_find_no_dups

# Carapace suggestions
zstyle ':completion:*' format $'\e[2;37mCompleting %d\e[m'
